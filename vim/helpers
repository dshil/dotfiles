" Executes an input command and saves the current state.
fun! ExecAndSave(cmd)
    if &binary || &filetype == 'diff'
        return
    endif

    " Save the last search.
    let _s=@/

    " Save the cursor position.
    let pos=getpos(".")

    " Exec a command.
    execute a:cmd

    " Restore the search and the cursor position.
    let @/=_s
    call setpos(".", pos)
endfun

fun! SquashBlankLines()
    call ExecAndSave("silent! :g/^\\_$\\n\\_^$/d")
endfun

fun! StripTrailingWhiteSpaces()
    call ExecAndSave("%s/\\s\\+$//e")
endfun

" Tab/Space switchers {{{
fun! SetupSpaces()
    set tabstop=4 softtabstop=4 shiftwidth=4 expandtab
endfun

fun! SetupTabs()
    set tabstop=4 softtabstop=4 shiftwidth=4 noexpandtab
endfun
" }}}

" Color scheme modes {{{

fun! SetCodeModeColor()
    let g:seoul256_background = 233
    colorscheme seoul256
    set background=dark
    highlight colorcolumn ctermbg=236
    highlight statusline ctermfg=236 ctermbg=15
    highlight MatchParen ctermbg=blue
endfun

fun! SetWriteModeColor()
    let g:seoul256_background = 240
    colorscheme seoul256
    set background=light
    highlight statusline ctermfg=236 ctermbg=15
    highlight MatchParen ctermbg=blue
endfun

" }}}

" vim:syntax=vim:foldmethod=marker:foldlevel=0
